package compilador;

import java_cup.runtime.*;
import ast.*;

action code {:
	private boolean modoDepuracion=false;
	private NodoBase ast=null;        
	
	//Debe ser llamada solo despues de un parse(), en caso contrario retornara null siempre;
	public NodoBase getASTroot(){
		return ast;
	}
:}

/* Simbolos Terminales */

terminal IF;
terminal THEN;
terminal ELSE;
terminal END;
terminal REPEAT;
terminal UNTIL;
terminal READ;
terminal WRITE;
terminal String ID;
terminal Integer NUM;
terminal ASSIGN;
terminal EQ;
terminal LT;
terminal PLUS;
terminal MINUS;
terminal TIMES;
terminal OVER;
terminal LPAREN;
terminal RPAREN;
terminal SEMI;
terminal GT;
terminal NE;
terminal GE;
terminal LE;
terminal FOR;
terminal BEGIN;
terminal AND;
terminal OR;
terminal LSB;
terminal RSB;
terminal BOOL;
terminal INT;
terminal VOID;
terminal COMMA;
terminal RETURN;
terminal TRUE;
terminal FALSE;

/* Simbolos No Terminales */
non terminal program;
non terminal NodoBase program_struct;
non terminal NodoBase main_program;
non terminal NodoBase funcs_seq;
non terminal NodoBase func;
non terminal tipoVariable func_type;
non terminal NodoBase func_args;
non terminal tipoVariable var_type;
non terminal NodoBase stmt_seq;
non terminal NodoBase stmt;
non terminal NodoBase if_stmt;
non terminal NodoBase repeat_stmt;
non terminal NodoBase assign_stmt;
non terminal NodoBase read_stmt;
non terminal NodoBase write_stmt;
non terminal NodoBase decl_stmt;
non terminal NodoBase decl_var;
non terminal NodoBase decl_name;
non terminal NodoBase func_call;
non terminal NodoBase call_args;
non terminal NodoBase for_stmt;
non terminal NodoBase return_stmt;
non terminal NodoBase exp;
non terminal NodoBase log_exp;
non terminal NodoBase cmp_exp;
non terminal NodoBase simple_exp;
non terminal NodoBase term;
non terminal NodoBase factor;

/* Precedencia de operadores */
precedence left PLUS;
precedence left MINUS;
precedence left TIMES;


/* REGLAS GRAMATICALES */
program ::= program_struct:ps
                {:
                    if(modoDepuracion)
                        System.out.println("\t regla 1"); 
                    ast = ps;                    
                :};

program_struct ::= funcs_seq:fs main_program:mp
                    {:
                        if(modoDepuracion)
                            System.out.println("\t regla 2a");
                        NodoBase nodoActual = fs;
                        if(nodoActual != null)
                        {
                            while(nodoActual.TieneHermano())
                            {
                                nodoActual = nodoActual.getHermanoDerecha();
                            }
                            nodoActual.setHermanoDerecha(mp);
                            RESULT = fs;
                        }    
                        else
                        {
                            RESULT = mp;
                        }
                    :}
                | main_program:mp
                    {:
                        if(modoDepuracion)
                            System.out.println("\t regla 2b");
                        RESULT = mp;
                    :};

main_program ::= BEGIN stmt_seq:ss END
                    {:
                        if(modoDepuracion)
                            System.out.println("\t regla 3");
                        NodoFuncion nodo = new NodoFuncion(ss);
                        ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                        RESULT = nodo;
                    :};

funcs_seq ::= funcs_seq:fs func:ft
                {:
                    if(modoDepuracion)
                        System.out.println("\t regla 4a");
                    NodoBase nodoActual = fs;
                    if(nodoActual != null)
                    {
                        while(nodoActual.TieneHermano())
                        {
                            nodoActual = nodoActual.getHermanoDerecha();
                        }
                        nodoActual.setHermanoDerecha(ft);
                        RESULT = fs;
                    }
                    else
                    {
                        RESULT = ft;
                    }
                :}
            | func:ft
                {:
                    if(modoDepuracion)
                        System.out.println("\t regla 4b");
                    RESULT = ft;
                :};

func ::= func_type:ft ID:id LPAREN func_args:fa RPAREN BEGIN stmt_seq:ss END
            {:
                if(modoDepuracion)
                    System.out.println("\t regla 5a");
                NodoFuncion nodo = new NodoFuncion(ft, ss, fa, id);
                ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                RESULT = nodo;
            :}
        | func_type:ft ID:id LPAREN RPAREN BEGIN stmt_seq:ss END
            {:
                if(modoDepuracion)
                    System.out.println("\t regla 5b");
                NodoFuncion nodo = new NodoFuncion(ft, ss, id);
                ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                RESULT = nodo;
            :};

func_type ::= var_type:vt
                {:
                    if(modoDepuracion)
                        System.out.println("\t regla 6a");
                    RESULT = vt;
                :}
            | VOID
                {:
                    if(modoDepuracion)
                        System.out.println("\t regla 6b");
                    RESULT = tipoVariable.vacio;
                :};

func_args ::= func_args:fa COMMA var_type:vt ID:id
                {:
                    if(modoDepuracion)
                        System.out.println("\t regla 7a");
                    NodoBase nodoActual = fa;
                    NodoDeclaracion nodoSiguiente = new NodoDeclaracion(new NodoVariable(id), vt);
                    if(nodoActual != null)
                    {
                        while(nodoActual.TieneHermano())
                        {
                            nodoActual = nodoActual.getHermanoDerecha();
                        }                        
                        nodoActual.setHermanoDerecha(nodoSiguiente);
                        RESULT = fa;
                    }
                    else
                    {
                        RESULT = nodoSiguiente;
                    }                    
                :}
            | var_type:vt ID:id
                {:
                    if(modoDepuracion)
                        System.out.println("\t regla 7b");
                    NodoDeclaracion  nodo = new NodoDeclaracion(new NodoVariable(id), vt);
                    ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                    RESULT = nodo;
                :};

var_type ::= BOOL 
                {:
                    if(modoDepuracion)
                        System.out.println("\t regla 8a");
                    RESULT = tipoVariable.booleano;
                :}
            | INT
                {:
                    if(modoDepuracion)
                        System.out.println("\t regla 8b");
                    RESULT = tipoVariable.entero;
                :};

stmt_seq ::= stmt_seq:ss stmt:sm SEMI 
                {:
                    if(modoDepuracion)
                        System.out.println("\t regla 9a");
                    NodoBase nodoActual = ss;
                    if(nodoActual != null)
                    {
                        while(nodoActual.TieneHermano())
                        {
                            nodoActual = nodoActual.getHermanoDerecha();
                        }
                        nodoActual.setHermanoDerecha(sm);
                        RESULT = ss;
                    }
                    else
                    {
                        RESULT = sm;
                    }
                :}
            | stmt:sm SEMI
                {:
                    if(modoDepuracion)
                        System.out.println("\t regla 9b");
                    RESULT = sm;
                :};

stmt ::= if_stmt:is 
            {:
                if(modoDepuracion)
                        System.out.println("\t regla 10a");                
                RESULT = is;
            :}
        | repeat_stmt:rs 
            {:
                if(modoDepuracion)
                        System.out.println("\t regla 10b");                
                RESULT = rs;
            :}
        | assign_stmt:as 
            {:
                if(modoDepuracion)
                        System.out.println("\t regla 10c");                
                RESULT = as;
            :}
        | read_stmt:rs 
            {:
                if(modoDepuracion)
                        System.out.println("\t regla 10d");                
                RESULT = rs;
            :}
        | write_stmt:ws
            {:
                if(modoDepuracion)
                        System.out.println("\t regla 10e");                
                RESULT = ws;
            :}
        | decl_stmt:ds 
            {:
                if(modoDepuracion)
                        System.out.println("\t regla 10f");                
                RESULT = ds;
            :}
        | func_call:fc
            {:
                if(modoDepuracion)
                        System.out.println("\t regla 10g");                
                RESULT = fc;
            :}
        | for_stmt:fs
            {:
                if(modoDepuracion)
                        System.out.println("\t regla 10h");                
                RESULT = fs;
            :}
        | return_stmt:rs 
            {:
                if(modoDepuracion)
                        System.out.println("\t regla 10i");                
                RESULT = rs;
            :}
        | error
            {:
                if(modoDepuracion)
                        System.out.println("\t regla 10j");
                System.out.println("Error sintáctico. Línea " + (((Scanner)parser.getScanner()).getLine() - 1) + ".");
                System.exit(0);
            :};

if_stmt ::= IF exp:ep THEN stmt_seq:ss END 
                {:                    
                    if(modoDepuracion)
                        System.out.println("\t regla 11a");
                    NodoIf nodo = new NodoIf(ep, ss);
                    ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                    RESULT = nodo;                    
                :}
            | IF exp:ep THEN stmt_seq:ss ELSE stmt_seq:ss2 END
                {:
                    if(modoDepuracion)
                        System.out.println("\t regla 11b");
                    NodoIf nodo = new NodoIf(ep, ss, ss2);
                    ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                    RESULT = nodo;
                :};

repeat_stmt ::= REPEAT stmt_seq:ss UNTIL exp:ep
            {:
                if(modoDepuracion)
                    System.out.println("\t regla 12");
                NodoRepeat nodo = new NodoRepeat(ss, ep);
                ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                RESULT = nodo;                
            :};

assign_stmt ::= exp:ep ASSIGN exp:ep2
            {:
                if (!(ep instanceof NodoIdentificador))
                {
                    System.out.println("Error sintáctico en la línea " + ((Scanner)parser.getScanner()).getLine() + ".");
                    System.exit(0);
                }
                if(modoDepuracion)
                    System.out.println("\t regla 13");
                NodoAsignacion nodo = new NodoAsignacion(ep, ep2);
                ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                RESULT = nodo;
            :};

read_stmt ::= READ ID:id
            {:
                if(modoDepuracion)
                    System.out.println("\t regla 14");
                NodoLeer nodo = new NodoLeer(id);
                ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                RESULT = nodo;
            :};

write_stmt ::= WRITE exp:ep
            {:
                if(modoDepuracion)
                    System.out.println("\t regla 15");
                NodoEscribir nodo = new NodoEscribir(ep);
                ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                RESULT = nodo;
            :};

decl_stmt ::= var_type:vt decl_var:dv
            {:
                if(modoDepuracion)
                    System.out.println("\t regla 16");
                NodoDeclaracion nodo = new NodoDeclaracion(dv, vt);
                ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                RESULT = nodo;
            :};

decl_var ::= decl_var:dv COMMA decl_name:dn 
                {:
                    if(modoDepuracion)
                        System.out.println("\t regla 17");
                    NodoBase nodoActual = dv;
                    if(dv != null)
                    {
                        while(nodoActual.TieneHermano())
                        {
                            nodoActual = nodoActual.getHermanoDerecha();
                        }
                        nodoActual.setHermanoDerecha(dn);
                        RESULT = dv;
                    }
                    else
                    {
                        RESULT = dn;
                    }                    
                :}
            | decl_name:dn
                {:
                    if(modoDepuracion)
                        System.out.println("\t regla 18");
                    RESULT = dn;
                    if(dn == null)
                    {
                        RESULT = null;
                    }
                :};

decl_name ::= ID:id LSB NUM:nm RSB 
                {:
                    if(modoDepuracion)
                        System.out.println("\t regla 19a");
                    NodoVariable nodo = new NodoVariable(id, nm);
                    ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                    RESULT = nodo;
                :}
            | ID:id
                {:
                    if(modoDepuracion)
                        System.out.println("\t regla 19b");
                    NodoVariable nodo = new NodoVariable(id);
                    ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                    RESULT = nodo;
                :};

func_call ::= ID:id LPAREN call_args:ca RPAREN 
                {:
                    if(modoDepuracion)
                        System.out.println("\t regla 20a");
                    NodoLlamada nodo = new NodoLlamada(id, ca);
                    ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                    RESULT = nodo;
                :}
            | ID:id LPAREN RPAREN
                {:
                    if(modoDepuracion)
                        System.out.println("\t regla 20b");
                    NodoLlamada nodo = new NodoLlamada(id);
                    ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                    RESULT = nodo;
                :};

call_args ::= call_args:ca COMMA exp:ep
                {:
                    if(modoDepuracion)
                        System.out.println("\t regla 21a");
                    NodoBase nodoActual = ca;
                    if(nodoActual != null)
                    {
                        while(nodoActual.TieneHermano())
                        {
                            nodoActual = nodoActual.getHermanoDerecha();
                        }
                        nodoActual.setHermanoDerecha(ep);
                        RESULT = ca;
                    }
                    else
                    {
                        RESULT = ep;
                    }
                :}
            | exp:ep
                {:
                    if(modoDepuracion)
                        System.out.println("\t regla 21b");
                    RESULT = ep;
                :};

for_stmt ::= FOR LPAREN assign_stmt:as SEMI exp:ep SEMI assign_stmt:as2 RPAREN stmt_seq:ss END
            {:
                if(modoDepuracion)
                    System.out.println("\t regla 22");
                NodoFor nodo = new NodoFor(as, ep, as2, ss);
                ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                RESULT = nodo;
            :};

return_stmt ::= RETURN exp:ep
            {:
                if(modoDepuracion)
                    System.out.println("\t regla 23");
                NodoReturn nodo = new NodoReturn(ep);
                ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                RESULT = nodo;
            :};

exp ::= exp:ep OR log_exp:le 
        {:
            if(modoDepuracion)
                System.out.println("\t regla 24a");
            NodoOperacion nodo = new NodoOperacion(ep, tipoOp.ologico, le);
            ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
            RESULT = nodo;
        :}
    | log_exp:le
        {:
            if(modoDepuracion)
                    System.out.println("\t regla 24b");
            RESULT = le;            
        :};

log_exp ::= log_exp:le AND cmp_exp:ce 
            {:
                if(modoDepuracion)
                    System.out.println("\t regla 25a");
                NodoOperacion nodo = new NodoOperacion(le, tipoOp.ylogico, ce);
                ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                RESULT = nodo;
            :}
        | cmp_exp:ce
            {:
                if(modoDepuracion)
                    System.out.println("\t regla 25b");
                RESULT = ce;                
            :};

cmp_exp ::= simple_exp:se LT simple_exp:se2 
            {:
                if(modoDepuracion)
                    System.out.println("\t regla 26a");
                NodoOperacion nodo = new NodoOperacion(se, tipoOp.menor, se2);
                ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                RESULT = nodo;
            :}
        | simple_exp:se EQ simple_exp:se2 
            {:
                if(modoDepuracion)
                    System.out.println("\t regla 26b");
                NodoOperacion nodo = new NodoOperacion(se, tipoOp.igual, se2);
                ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                RESULT = nodo;                
            :}
        | simple_exp:se GT simple_exp:se2 
            {:
                if(modoDepuracion)
                    System.out.println("\t regla 26c");
                NodoOperacion nodo = new NodoOperacion(se, tipoOp.mayor, se2);
                ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                RESULT = nodo;
            :}
        | simple_exp:se NE simple_exp:se2 
            {:
                if(modoDepuracion)
                    System.out.println("\t regla 26d");
                NodoOperacion nodo = new NodoOperacion(se, tipoOp.diferente, se2);
                ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                RESULT = nodo;
            :}
        | simple_exp:se GE simple_exp:se2 
            {:
                if(modoDepuracion)
                    System.out.println("\t regla 26e");
                NodoOperacion nodo = new NodoOperacion(se, tipoOp.mayorigual, se2);
                ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                RESULT = nodo;
            :}
        | simple_exp:se LE simple_exp:se2 
            {:
                if(modoDepuracion)
                    System.out.println("\t regla 26f");
                NodoOperacion nodo = new NodoOperacion(se, tipoOp.menorigual, se2);
                ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                RESULT = nodo;
            :}
        | simple_exp:se        
            {:
                if(modoDepuracion)
                    System.out.println("\t regla 26g");
                RESULT = se;
            :};

simple_exp ::= simple_exp:se PLUS term:tm 
                {:
                    if(modoDepuracion)
                        System.out.println("\t regla 27a");
                    NodoOperacion nodo = new NodoOperacion(se, tipoOp.mas, tm);
                    ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                    RESULT = nodo;
                :}
            | simple_exp:se MINUS term:tm 
                {:
                    if(modoDepuracion)
                        System.out.println("\t regla 27b");
                    NodoOperacion nodo = new NodoOperacion(se, tipoOp.menos, tm);
                    ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                    RESULT = nodo;
                :}
            | term:tm
                {:
                    if(modoDepuracion)
                        System.out.println("\t regla 27c");
                    RESULT = tm;                    
                :};

term ::= term:tm TIMES factor:ft 
            {:
                if(modoDepuracion)
                    System.out.println("\t regla 28a");
                NodoOperacion nodo = new NodoOperacion(tm, tipoOp.por, ft);
                ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                RESULT = nodo;
            :}
        | term:tm OVER factor:ft 
            {:
                if(modoDepuracion)
                    System.out.println("\t regla 28b");
                NodoOperacion nodo = new NodoOperacion(tm, tipoOp.entre, ft);
                ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                RESULT = nodo;
            :}
        | factor:ft
            {:
                if(modoDepuracion)
                    System.out.println("\t regla 28c");
                RESULT = ft;
            :};

factor ::= LPAREN exp:ep RPAREN 
            {:
                if(modoDepuracion)
                    System.out.println("\t regla 29a");
                RESULT = ep;
            :}
        | func_call:fc 
            {:
                if(modoDepuracion)
                    System.out.println("\t regla 29b");
                RESULT = fc;
            :}
        | NUM:nm 
            {:
                if(modoDepuracion)
                    System.out.println("\t regla 29c");
                NodoValor nodo = new NodoValor(nm, tipoVariable.entero);
                ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                RESULT = nodo;
            :}
        | ID:id 
            {:
                if(modoDepuracion)
                    System.out.println("\t regla 29d");
                NodoIdentificador nodo = new NodoIdentificador(id);
                ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                RESULT = nodo;
            :}
        | ID:id LSB exp:ep RSB
            {:
                if(modoDepuracion)
                    System.out.println("\t regla 29e");
                NodoIdentificador nodo = new NodoIdentificador(id, ep);
                ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                RESULT = nodo;
            :}
        | TRUE 
            {:
                if(modoDepuracion)
                    System.out.println("\t regla 29f");
                NodoValor nodo = new NodoValor(true, tipoVariable.booleano);
                ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                RESULT = nodo;
            :}
        | FALSE 
            {:
                if(modoDepuracion)
                    System.out.println("\t regla 29g");
                NodoValor nodo = new NodoValor(false, tipoVariable.booleano);
                ((NodoBase) nodo).setNumeroLinea(((Scanner)parser.getScanner()).getLine());
                RESULT = nodo;
            :}
        | error
            {:
                if(modoDepuracion)
                    System.out.println("\t regla 29h");
                System.out.println("Error sintáctico. Línea " + (((Scanner)parser.getScanner()).getLine() - 1) + ".");
                System.exit(0);
            :};